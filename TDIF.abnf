;;; file

file            = *comment header *(comment / record)

;;; header and record

header          = value *(comma value) linebreak

record          = field *(comma field) linebreak

field           = null / value

value           = DQUOTE *(textdata / 2DQUOTE) DQUOTE

textdata        = %x00-21 / %x23-7F / UTF8-data
                    ; all characters except quotation mark

;;; comment

comment         = hash *commentdata linebreak

commentdata     = %x00-09 / %x0B-0C / %x0E-7F / UTF8-data
                    ; all characters except LF, CR

;;; Common rules

null            = %x5C.4E       ; \N

comma           = %x2C          ; ,

hash            = %x23          ; #

linebreak       = CR / LF / CRLF

;;; Basic rules

LF              = %x0A
                    ; as per section B.1 of [RFC5234]

CR              = %x0D
                    ; as per section B.1 of [RFC5234]

CRLF            = CR LF
                    ; as per section B.1 of [RFC5234]

DQUOTE          = %x22
                    ; as per section B.1 of [RFC5234]

UTF8-data       = UTF8-2 / UTF8-3 / UTF8-4
                    ; as per section 4 of [RFC3629]
